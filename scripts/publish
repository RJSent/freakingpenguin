#!/usr/bin/env sh
:; set -e # -*- mode: emacs-lisp; lexical-binding: t -*-
:; emacs --no-site-file --script "$0" -- "$@" || __EXITCODE=$?
:; exit ${__EXITCODE:-0}
;; This code was taken from michaeljherold.com/articles/using-ox-hugo-without-duplication/
(defvar bootstrap-version)
(defvar straight-base-dir)
;; (defvar straight-fix-org)
(defvar straight-vc-git-default-clone-depth 1)
(defvar publish--straight-repos-dir)
(defvar org-confirm-babel-evaluate)
(defvar ob-mermaid-cli-path)
(defvar richard-babel-languages-to-eval (list 'mermaid))

(setq gc-cons-threshold 83886080 ; 80MiB
      straight-base-dir (expand-file-name "../.." (or load-file-name buffer-file-name))
      ;; straight-fix-org t
      straight-vc-git-default-clone-depth 1
      publish--straight-repos-dir (expand-file-name "straight/repos/" straight-base-dir))

(let ((bootstrap-file (expand-file-name "straight/repos/straight.el/bootstrap.el" straight-base-dir))
      (bootstrap-version 5))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
	(url-retrieve-synchronously
	 "https://raw.githubusercontent.com/raxod502/straight.el/develop/install.el"
	 'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))

(message "Downloading dependencies...")
;; ox-hugo clones org as dependency, redundant (also org != org-mode apparently??)
;; (straight-use-package
;;  '(org-mode :type git
;; 	    :host github
;; 	    :repo "emacs-straight/org-mode"
;; 	    :files ("*.el" "lisp/*.el" "contrib/lisp/*.el")))
(straight-use-package
 '(ox-hugo :type git
	   :host github
	   :repo "kaushalmodi/ox-hugo"
	   :nonrecursive t))
(straight-use-package
 '(ob-mermaid :type git
	      :host github
	      :repo "arnm/ob-mermaid"
	      :nonrecursive t))
;; TODO: Automate install of mermaid-cli, npm install
;; @mermaid-js/mermaid-cli, which requires install of npm
;; Also find solution to potentially different locations of
;; node_modules
(if (file-directory-p "~/.node_modules")
    (setq ob-mermaid-cli-path "~/.node_modules/.bin/mmdc")
  (setq ob-mermaid-cli-path "~/node_modules/.bin/mmdc"))

(with-current-buffer (find-file-noselect (expand-file-name "blog.org" "."))
  (org-next-visible-heading 1)

  (when (equal (org-entry-get (point) "CUSTOM_ID") "toc")
    (let ((inhibit-message t))
      (org-cut-subtree)))

  ;; Not needed since I figure out the issue, see comment above
  ;; org-babel-do-load-languages. No harm to keep it though. Security,
  ;; right? (Don't ask "security from what?")
  ;; Also keeping this would allow for more complex conditional evaluation later if I ever have the need.
  (message "Prepping to eval code blocks...")
  (defadvice org-babel-execute-src-block (around conditional-eval nil activate)
    "Execute src-block if the block's language is in richard-babel-languages-to-eval"
    (let ((language (org-element-property :language (org-element-at-point))))
      (if (member (intern language) richard-babel-languages-to-eval)
	  ad-do-it)))
  ;; TODO: Use richard-babel-languages-to-eval
  ;; Turns out I'm a gigantic idiot as to why org export wasn't
  ;; evaluating my source blocks. (mermaid . t) was not a part of
  ;; Emacs when running the script, so it wouldn't eval mermaid
  ;; blocks.
  (org-babel-do-load-languages
   'org-babel-load-languages
   '((mermaid . t)))
  ;; TODO: For security replace nil with function which "takes two
  ;; arguments the language of the code block and the body of the code
  ;; block. Such a function should then return a non-nil value if the
  ;; user should be prompted for execution or nil if no prompt is
  ;; required."
  (setq org-confirm-babel-evaluate nil)

  (message "Publishing...")
  ;; Remove hugo's URL sanitization as Goldmark is now used instead of
  ;; Blackfriday, not needed. This behavior leads to 404 errors on
  ;; certain websites and browsers, see
  ;; https://github.com/kaushalmodi/ox-hugo/issues/236
  ;; and a pull request
  ;; https://github.com/kaushalmodi/ox-hugo/pull/403
  (defun url-sanitize-overwrite (orig-fun &rest args)
    ;; Careful, args is list of arguments, not the argument itself
    (identity (nth 0 args)))
  (advice-add 'org-blackfriday--url-sanitize :around #'url-sanitize-overwrite)
  
  (org-hugo-export-wim-to-md t))
